buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'net.saliman:gradle-cobertura-plugin:2.3.2'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.7.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath 'me.champeau.gradle:jmh-gradle-plugin:0.3.1'
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.5.3"
        classpath "org.ajoberstar:gradle-git:1.3.2"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.0.0"
        classpath 'com.github.erizo.gradle:jcstress-gradle-plugin:0.8.0'
    }
}

apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.jfrog.bintray'

ext {
    releaseVersion = '0.8.2'
    javaslangVersion = '2.0.5'
    rxJavaVersion = '2.0.6'
}


allprojects {
    apply plugin: 'java'
    apply plugin: 'net.saliman.cobertura'
    apply plugin: 'me.champeau.gradle.jmh'
    apply plugin: 'com.jfrog.artifactory'

    version = '0.9-0-SNAPSHOT'
    group = 'io.github.robwin'
    description = 'A lightweight, easy-to-use fault tolerance library designed for Java8 and functional programming'

    repositories {
        jcenter()
        mavenCentral()
    }
}
artifactoryPublish.skip=true // apply to all projects except the root

subprojects {
    apply plugin: 'maven'

    dependencies {
        compile "io.javaslang:javaslang:${javaslangVersion}"
        compile "org.slf4j:slf4j-api:1.7.24"

        testCompile "io.dropwizard.metrics:metrics-core:3.1.2"
        testCompile "junit:junit:4.11"
        testCompile "org.assertj:assertj-core:3.6.2"
        testCompile "ch.qos.logback:logback-classic:0.9.26"
        testCompile "io.dropwizard.metrics:metrics-healthchecks:3.1.2"
        testCompile "org.mockito:mockito-core:1.10.19"
        testCompile "org.powermock:powermock:1.6.6"
        testCompile "org.powermock:powermock-api-mockito:1.6.6"
        testCompile "org.powermock:powermock-module-junit4:1.6.6"
        testCompile "com.jayway.awaitility:awaitility:1.7.0"

        jmh "ch.qos.logback:logback-classic:0.9.26"
        jmh "org.openjdk.jmh:jmh-generator-annprocess:1.12"
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifactoryPublish {
        dependsOn sourceJar, javadocJar
    }

    configurations {
        published
    }

    artifacts {
        published sourceJar
        published javadocJar
    }

    cobertura {
        coverageIgnoreTrivial = true
    }

    jmh {
        duplicateClassesStrategy = 'warn'
        jmhVersion = '1.17'
    }
}

configurations {
    published
}

def files = subprojects.collect { new File(it.projectDir, '/build/cobertura/cobertura.ser') }
cobertura {
    coverageFormats = ['html', 'xml']
    coverageSourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
    coverageMergeDatafiles = files
}

test {
    dependsOn(subprojects.test) // required by cobertura to aggregate report
}

tasks.coveralls {
    dependsOn 'check'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2'
}

artifactory {
    contextUrl = 'https://oss.jfrog.org'
    resolve {
        repository {
            repoKey = 'libs-release'
            maven = true
        }
    }
    publish {
        repository {
            repoKey = 'oss-snapshot-local' //The Artifactory repository key to publish to
            //when using oss.jfrog.org the credentials are from Bintray. For local build we expect them to be found in
            //~/.gradle/gradle.properties, otherwise to be set in the build server
            username = project.hasProperty('bintrayUsername') ? project.bintrayUsername : System.getenv('BINTRAY_USER')
            password = project.hasProperty('bintrayApiKey') ? project.bintrayApiKey : System.getenv('BINTRAY_KEY')
        }
        defaults {
            publishConfigs('published')
        }
    }
    if (System.properties['https.proxyHost']) {
        clientConfig.proxy.host = System.properties['https.proxyHost']
        clientConfig.proxy.port = System.properties['https.proxyPort'].toInteger()
    }
}


//publishing {
//    publications {
//        mavenJava(MavenPublication) {
//            from components.java
//            pom.withXml {
//                def devs = ['RobWin': 'Robert Winkler']
//                def root = asNode()
//
//                root.dependencies.'*'.findAll() {
//                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
//                        dep.name == it.artifactId.text()
//                    }
//                }.each() {
//                    it.scope*.value = 'compile'
//                }
//
//                root.appendNode('name', 'javaslang-circuitbreaker')
//                root.appendNode('packaging', 'jar')
//                root.appendNode('url', 'https://github.com/RobWin/javaslang-circuitbreaker')
//                root.appendNode('description', 'A CircuitBreaker pattern implementation for Java8 and functional programming .')
//
//                def license = root.appendNode('licenses').appendNode('license')
//                license.appendNode('name', 'Apache-2.0')
//                license.appendNode('url', 'https://github.com/RobWin/javaslang-circuitbreaker/blob/master/LICENSE.txt')
//                license.appendNode('distribution', 'repo')
//
//                root.appendNode('scm').appendNode('url', 'https://github.com/RobWin/javaslang-circuitbreaker.git')
//
//                def developers = root.appendNode('developers')
//                devs.each {
//                    def d = developers.appendNode('developer')
//                    d.appendNode('id', it.key)
//                    d.appendNode('name', it.value)
//                }
//            }
//            artifact sourceJar
//            artifact javadocJar
//        }
//    }
//
//}